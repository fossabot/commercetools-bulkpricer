buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'se.transmode.gradle:gradle-docker:1.2'
  }
}

plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'docker'
apply plugin: 'java'

group = 'nkuehn'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.5.1'
def eclipseCollectionsVersion = '9.1.0'
def commercetoolsJvmSdkVersion = "1.30.0"
def mainVerticleName = 'com.commercetools.bulkpricer.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

docker {
  baseImage 'vertx/vertx3-exec'
  // maintainer 'Nikolaus KÃ¼hn "nikolaus.kuehn@commercetools.de"'
}

task buildDocker(type: Docker) {
  tagVersion = System.getenv('BUILD_NUMBER') ?: project.version
  push = Boolean.getBoolean('docker.push')
  applicationName = 'commercetools-bulkpricer'

  addFile {
    from "${project.shadowJar.outputs.files.singleFile}"
    into "/opt/$applicationName/"
  }
  exposePort 8080
  entryPoint = ["sh",  "-c"]
  defaultCommand = ["java -jar /opt/$applicationName/${project.name}-${project.version}-fat.jar"]
}
buildDocker.group = 'docker'

repositories {
  mavenCentral()
  jcenter()
}

dependencies {

  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile "org.slf4j:slf4j-jdk14:1.7.25"

  compile "org.eclipse.collections:eclipse-collections-api:$eclipseCollectionsVersion"
  compile "org.eclipse.collections:eclipse-collections:$eclipseCollectionsVersion"

  // pull in an own SDK locally
  compile files ("../commercetools-jvm-sdk/commercetools-sdk-base/target/commercetools-sdk-base-1.31.0-SNAPSHOT.jar")

  compile "com.commercetools.sdk.jvm.core:commercetools-models:$commercetoolsJvmSdkVersion"
  compile "com.commercetools.sdk.jvm.core:commercetools-java-client-apache-async:$commercetoolsJvmSdkVersion"
  compile "com.commercetools.sdk.jvm.core:commercetools-convenience:$commercetoolsJvmSdkVersion"

  testCompile "junit:junit:4.12"
  testCompile "io.vertx:vertx-unit:$vertxVersion"
  testCompile "io.vertx:vertx-web-client:$vertxVersion"
}

tasks.withType(JavaCompile) {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  // this subproject requires -parameters option (Jackson deserialization parameters)
  options.compilerArgs << '-parameters'
}

shadowJar {
  classifier = 'fat'
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

// for heroku:
task stage(dependsOn: ['assembleShadowDist'])

task wrapper(type: Wrapper) {
  gradleVersion = '4.5.1'
}


